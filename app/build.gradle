apply plugin: 'com.android.application'

import com.dicio.sentences_compiler.main.SentencesCompiler

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.dicio.dicio_android"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

task sentencesCompiler {
    doFirst {
        File inputDirectory = file("src/main/sentences/")
        inputDirectory.mkdirs()
        File outputDirectory = file("src/main/java/com/dicio/dicio_android/")
        outputDirectory.mkdirs()
        File outputFile = file("$outputDirectory/Sentences.java")

        List<String> arguments = new ArrayList<String>()
        File[] files = inputDirectory.listFiles()
        if (files.length == 0) {
            return
        }

        for (File file : files) {
            arguments.add("-i")
            arguments.add("UTF-8:" + file.absolutePath)
        }
        arguments.add("-o")
        arguments.add("UTF-8:" + outputFile.absolutePath)

        arguments.add("java")
        arguments.add("--package")
        arguments.add("\"com.dicio.dicio_android\"")
        arguments.add("--class")
        arguments.add("\"Sentences\"")

        SentencesCompiler sentencesCompiler = new SentencesCompiler()
        sentencesCompiler.main((String[])arguments.toArray())
    }
}
preBuild.dependsOn(sentencesCompiler)


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'com.github.Stypox:dicio-assistance-component:490fd2b949bbff13ba97458b8bbd5e87134c0139'
    implementation 'com.squareup.picasso:picasso:2.71828'
}
